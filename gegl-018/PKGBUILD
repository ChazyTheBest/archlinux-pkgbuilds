# Maintainer: Benjamin A. Shelton <zancarius@gmail.com>
# Based on the official PKGBUILD by Eric BÃ©langer <eric@archlinux.org>

pkgname=gegl-018
pkgver=0.1.8
pkgrel=2
pkgdesc="Graph based image processing framework"
arch=('i686' 'x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'gtk2')
conflicts=('gegl')
provides=('gegl')
makedepends=('ruby' 'lua' 'openexr' 'ffmpeg' 'librsvg' 'jasper' 'exiv2')
optdepends=('openexr: for using the openexr plugin' \
            'ffmpeg: for using the ffmpeg plugin' \
            'librsvg: for using the svg plugin' \
            'jasper: for using the jasper plugin')
options=('!libtool')
source=(ftp://ftp.gimp.org/pub/${pkgname/[-0-9]*/}/${pkgver%.*}/${pkgname/[-0-9]*/}-${pkgver}.tar.bz2)
sha1sums=('3d4d902580f5c5122b01b5188b6b5da03089ecac')

build() {
  cd "${srcdir}/${pkgname/[-0-9]*/}-${pkgver}"
  ./configure  --prefix=/usr  --with-sdl --with-openexr --with-librsvg \
    --with-libavformat --with-jasper --disable-docs
  make
}

check() {
  cd "${srcdir}/${pkgname/[-0-9]*/}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname/[-0-9]*/}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
