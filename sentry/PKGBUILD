# Maintainer: Benjamin A. Shelton <zancarius@gmail.com>
# Source: https://github.com/zancarius/archlinux-pkgbuilds

pkgname=sentry
pkgver=5.1.0
pkgrel=2
pkgdesc="Python-based realtime logging and aggregation server."
arch=(any)
url="http://pypi.python.org/pypi/sentry"
license=(BSD)
conflicts=('python2-sentry')
depends=(
    python2
)
makedepends=(git python2-distribute python2-virtualenv)
options=(!strip)
install="${pkgname}.install"
source=(
    "http://pypi.python.org/packages/source/s/${pkgname}/${pkgname}-${pkgver}.tar.gz"
    "${pkgname}.install"
    "${pkgname}.service"
)
md5sums=(
    a93687928f588f65ca55de18f86b912c # sentry tarball
    2c5f9de8bdc07895b765bb62a3aff7b4 # sentry.install
    7d90b474e728204d8954301440ac5d53 # sentry.service
)

build () {
    

    mkdir -p "${pkgdir}/opt/sentry"
    virtualenv2 "${pkgdir}/opt/sentry"

    "${pkgdir}/opt/sentry/bin/pip" install mysql-python
    "${pkgdir}/opt/sentry/bin/pip" install psycopg2

    cd "${srcdir}/${pkgname}-${pkgver}"    
    "${pkgdir}/opt/sentry/bin/python2" setup.py install --optimize=1

    # Remove Desktop Services Store from package. This has been removed
    # from django-paging's Github repo but still persists in the PIP
    # package. Harmless, but I don't like them. :) More information:
    # http://en.wikipedia.org/wiki/.DS_Store
    find "${pkgdir}" -name '.DS_Store' -delete

}

package () {

    install -Dm0644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm0644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/sentry.service"

}