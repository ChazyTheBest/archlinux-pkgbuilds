# Maintainer: Benjamin A. Shelton <zancarius@gmail.com>
# Source: https://github.com/zancarius/archlinux-pkgbuilds

pkgname=sentry
pkgver=5.4.5
pkgrel=2
pkgdesc="Python-based realtime logging and aggregation server."
arch=(any)
url="http://pypi.python.org/pypi/sentry"
license=(BSD)
conflicts=('python2-sentry')
depends=(
    python2
)
makedepends=(git python2-distribute python2-virtualenv)
options=(!strip)
install="${pkgname}.install"
source=(
    "http://pypi.python.org/packages/source/s/${pkgname}/${pkgname}-${pkgver}.tar.gz"
    "${pkgname}.install"
    "${pkgname}.service"
)
md5sums=(
    8c92260cc2b956edf8340b5634ba0b08 # sentry tarball
    e2bd1d37f543f26ae43fd9bc4e8e6444 # sentry.install
    ebb9875dedec90b5b9eda00fab49f25e # sentry.service
)

build () {

    mkdir -p "${pkgdir}/opt/sentry"
    virtualenv2 --python=python2.7 "${pkgdir}/opt/sentry"

    source "${pkgdir}/opt/sentry/bin/activate"

    # Workaround for circumstances where cssutils may not be installed
    # into the virtualenv at the correct version.
    "${pkgdir}/opt/sentry/bin/pip" install "cssutils>=0.9.9,<0.10.0"

    cd "${srcdir}/${pkgname}-${pkgver}"    
    "${pkgdir}/opt/sentry/bin/python2" setup.py install --optimize=1

    virtualenv2 --relocatable "${pkgdir}/opt/sentry"

    mkdir "${pkgdir}/opt/sentry/.python-eggs"

    # Remove Desktop Services Store from package. This has been removed
    # from django-paging's Github repo but still persists in the PIP
    # package. Harmless, but I don't like them. :) More information:
    # http://en.wikipedia.org/wiki/.DS_Store
    find "${pkgdir}" -name '.DS_Store' -delete

}

package () {

    install -Dm0644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm0644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/sentry.service"

}
