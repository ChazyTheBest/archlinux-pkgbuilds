# This is not the same php56-imagick package as is availabe in the AUR. This
# is a fork specifically for my PHP56 port that conflicts with the official
# PHP 7.x release. For more information, see the README contained in my PHP56
# package.

# Do not upload this package to the AUR under any circumstances.

# Contributor: Mladen Milinkovic <maxrd2@smoothware.net>
# Based on aur/php-imagick by Spencer Rinehart <anubis@overthemonkey.com>

pkgname=php-imagick
_extname=imagick
pkgver=3.1.2
pkgrel=4
pkgdesc="PHP extension for IMagick"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/${_extname}"
license=('PHP')
depends=('php56>=5.1.3' 'imagemagick>=6.2.4')
backup=("etc/php/conf.d/${_extname}.ini")
install="php-${_extname}.install"
source=("http://pecl.php.net/get/${_extname}-${pkgver}.tgz")
md5sums=('f2fd71b026debe056e0ec8d76c2ffe94')

build() {
  cd "${_extname}-${pkgver}"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "${_extname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  echo "extension=${_extname}.so" > "${_extname}.ini"
  install -D -m644 "${_extname}.ini" "${pkgdir}/etc/php/conf.d/${_extname}.ini"
}
