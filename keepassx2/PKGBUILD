# Maintainer: Benjamin A. Shelton <zancarius@gmail.com>
# Previous maintainer: Michael Laß <bevan@bi-co.net>
# Contributor: Marat "Morion" Talipov <morion.self@gmail.com>

# This PKGBUILD was previously maintained by Michael Laß on GitHub:
# https://github.com/michaellass/AUR

# It has since been removed from Mr. Laß' GitHub repository as
# well as the AUR. I will not be uploading a copy of this to the AUR4
# as I'm not sure how many other individuals are currently using this
# version of KeePassX2.


pkgname=keepassx2
pkgver=beta2
pkgrel=1
pkgdesc="Crossplatform port of Windows' application  ''KeePass Password Safe''"
arch=('i686' 'x86_64')
url="https://www.keepassx.org/dev/"
license=('GPL2' 'GPL3')
depends=('shared-mime-info' 'libxtst' 'qt4')
makedepends=('intltool' 'cmake')
conflicts=('keepassx' 'keepassx2-git')
options=(!emptydirs)
install=keepassx2.install
source=(
    # Tarball.
    "https://www.keepassx.org/dev/attachments/download/115/keepassx-2.0-${pkgver}.tar.gz"
    # Signature.
    "https://www.keepassx.org/dev/attachments/download/114/keepassx-2.0-${pkgver}.tar.gz.sig"
    )
md5sums=(
    '95114e6719d12eb9a1e3ac618b7bd275'
    '3e895e1428d6a4ed8f3799146af8682c'
)
validpgpkeys=(
    '164C70512F7929476764AB56FE22C6FD83135D45'
)

_cmake_keys="-DCMAKE_INSTALL_PREFIX=/usr
             -DCMAKE_INSTALL_LIBDIR=/usr/lib
             -DCMAKE_BUILD_TYPE=Release"

build() {
    cd "$srcdir/keepassx-2.0-$pkgver"

    if [[ -d build ]]; then
        rm -rf build
    fi
    mkdir build && cd build

    cmake $_cmake_keys ..
    make
}

package() {
    cd "$srcdir/keepassx-2.0-$pkgver/build"
    make DESTDIR="${pkgdir}" install
}
