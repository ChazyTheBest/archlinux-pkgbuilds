# Maintainer: Benjamin A. Shelton <zancarius@gmail.com>
# Based off the official Arch Linux package build by
# Ionut Biru <ibiru@archlinux.org> and Jakub Schmidtke <sjakub@gmail.com>
# with additions and patches from firefox-kde-opensuse PKGBUILD by
# Weng Xuetian <wengxt@gmail.com>.

pkgname=firefox-safebrowsing-opensuse
pkgver=14.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with openSuSE patches and SafeBrowsing enabled"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib'
         'libnotify' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'libevent' 'nss>=3.13.3' 'hunspell' 'sqlite')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'mesa'
             'autoconf2.13' 'libidl2' 'xorg-server-xvfb' 'imake' 'gcc')
optdepends=('wireless_tools: Location detection via available WiFi networks')
provides=('firefox=13.0.1')
conflicts=('firefox')
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
options=(!emptydirs)
source=(
        https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig
        firefox.desktop
        firefox-install-dir.patch
        vendor.js
        firefox-browser-css.patch
        firefox-kde.patch
        kde.js
        mozilla-kde.patch
        mozilla-nongnome-proxies.patch
        shared-libs.patch
)
md5sums=(
         'c2f884f0f6c41c65cf20f678a1ee7191'  # Firefox tarball
         '591d7173a7f5bc0ee01124fca80f3782'  # mozconfig
         'bdeb0380c7fae30dd0ead6d2d3bc5873'  # firefox.desktop
         '1e4bcac59e93d21fffa6a1d1ad235247'  # firefox-install-dir.patch
         '0d053487907de4376d67d8f499c5502b'  # vendor.js
         'b6fd821c02bde029a816ac043163dce0'  # firefox-browser-css.patch
         '424cb9453b89615fae4307e2b7b6942a'  # firefox-kde.patch
         '75df0f88cc7a7fa7d522459e4ff82cc5'  # kde.js
         '24ba4b7635ed12786fc1f2febf44d017'  # mozilla-kde.patch
         '8b0ecfdf697485d7b7dd26291c0dc478'  # mozilla-nongnome-proxies.patch
         '52e52f840a49eb1d14be1c0065b03a93'  # shared-libs.patch
)

build() {
  cd mozilla-release

  cp ../mozconfig .mozconfig
  install -m 644 "${srcdir}/kde.js" browser/app/profile/kde.js

  msg "Applying firefox-install-dir.patch"  
  patch -Np1 -i ../firefox-install-dir.patch
  msg "Applying mozilla-nongnome-proxies.patch"  
  patch -Np1 -i ../mozilla-nongnome-proxies.patch
  msg "Applying mozilla-kde.patch"  
  patch -Np1 -i ../mozilla-kde.patch
#  msg "Applying firefox-cross-desktop.patch"  
#  patch -Np1 -i ../firefox-cross-desktop.patch
  msg "Applying firefox-kde.patch"  
  patch -Np1 -i ../firefox-kde.patch
  msg "Applying firefox-browser-css.patch"  
  patch -Np1 -i ../firefox-browser-css.patch
  msg "Applying shared-libs.patch"  
  patch -Np1 -i ../shared-libs.patch

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  #export CC=gcc-4.6
  #export CXX=g++-4.6

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  # Enable PGO
  export MOZ_PGO=1
  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  make -f client.mk build

  kill $! || true
}

package() {
  cd mozilla-release
  make -j1 -f client.mk DESTDIR="$pkgdir" install

  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/pref"

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done

  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
